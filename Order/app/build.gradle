plugins {
    id 'com.jfrog.bintray' version '1.8.0'
    id 'com.github.dcendents.android-maven' version '2.0'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

group = 'com.github.moraisigor'
version = '0.10.0'

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'

    implementation 'com.cielo.lio:order-manager:1.5.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'

    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'

    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name ''
                description ''
                url ''

                licenses {
                    license {
                        name 'MIT'
                        url ''
                    }
                }
                developers {
                    developer {
                        id 'MoraisIgor'
                        name 'Igor Morais'
                        email 'igor.morais@hollowsoft.com'
                    }
                }
                scm {
                    url 'https://github.com/MoraisIgor/XOrderManager'
                    connection 'https://github.com/MoraisIgor/XOrderManager.git'
                    developerConnection 'https://github.com/MoraisIgor/XOrderManager.git'
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()

    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.key')

    publish = true

    pkg {
        repo = 'Maven'

        name = 'XOrderManager'
        desc = ''
        labels = ['android', 'kotlin']
        licenses = ['MIT']

        vcsUrl = 'https://github.com/MoraisIgor/XOrderManager.git'
        websiteUrl = 'https://github.com/MoraisIgor/XOrderManager'
        issueTrackerUrl = 'https://github.com/MoraisIgor/XOrderManager/issues'

        version {
            gpg {
                sign = true
                passphrase = properties.getProperty('bintray.password')
            }
        }
    }
}
